load("@com_google_protobuf//bazel:proto_library.bzl", "proto_library")
load("@grpc_ecosystem_grpc_gateway//protoc-gen-openapiv2:defs.bzl", "protoc_gen_openapiv2")
load("@io_bazel_rules_go//proto:def.bzl", "go_proto_library")
load(
    "@wfa_rules_kotlin_jvm//kotlin:defs.bzl",
    "kt_jvm_grpc_proto_library",
    "kt_jvm_proto_library",
)
load("//build:defs.bzl", "proto_lib_prefix")
load("//build/grpc_gateway:defs.bzl", "go_grpc_gateway_proto_library")

package(default_visibility = ["//visibility:public"])

exports_files(["permissions_config.textproto"])

IMPORT_PREFIX = "/src/main/proto"

# Resources and shared message types.

MESSAGE_LIBS = [
    ":basic_report_proto",
    ":event_filter_proto",
    ":event_group_proto",
    ":event_template_field_proto",
    ":impression_qualification_filter_proto",
    ":impression_qualification_filter_spec_proto",
    ":media_type_proto",
    ":metric_calculation_spec_proto",
    ":metric_frequency_spec_proto",
    ":metric_proto",
    ":result_group_proto",
    ":page_token_proto",
    ":report_proto",
    ":report_schedule_iteration_proto",
    ":report_schedule_proto",
    ":reporting_impression_qualification_filter_proto",
    ":reporting_interval_proto",
    ":reporting_set_proto",
    ":reporting_unit_proto",
    ":time_interval_proto",
]

proto_library(
    name = "basic_report_proto",
    srcs = ["basic_report.proto"],
    strip_import_prefix = IMPORT_PREFIX,
    deps = [
        ":reporting_impression_qualification_filter_proto",
        ":reporting_interval_proto",
        ":result_group_proto",
        "@com_google_googleapis//google/api:field_behavior_proto",
        "@com_google_googleapis//google/api:resource_proto",
        "@com_google_protobuf//:timestamp_proto",
    ],
)

proto_library(
    name = "event_filter_proto",
    srcs = ["event_filter.proto"],
    strip_import_prefix = IMPORT_PREFIX,
    deps = [
        ":event_template_field_proto",
        "@com_google_googleapis//google/api:field_behavior_proto",
    ],
)

proto_library(
    name = "event_group_proto",
    srcs = ["event_group.proto"],
    strip_import_prefix = IMPORT_PREFIX,
    deps = [
        ":media_type_proto",
        "@com_google_googleapis//google/api:field_behavior_proto",
        "@com_google_googleapis//google/api:resource_proto",
        "@com_google_googleapis//google/type:interval_proto",
        "@com_google_protobuf//:any_proto",
    ],
)

proto_library(
    name = "event_template_field_proto",
    srcs = ["event_template_field.proto"],
    strip_import_prefix = IMPORT_PREFIX,
    deps = [
        "@com_google_googleapis//google/api:field_behavior_proto",
    ],
)

proto_library(
    name = "impression_qualification_filter_proto",
    srcs = ["impression_qualification_filter.proto"],
    strip_import_prefix = IMPORT_PREFIX,
    deps = [
        ":impression_qualification_filter_spec_proto",
        "@com_google_googleapis//google/api:field_behavior_proto",
        "@com_google_googleapis//google/api:resource_proto",
    ],
)

proto_library(
    name = "impression_qualification_filter_spec_proto",
    srcs = ["impression_qualification_filter_spec.proto"],
    strip_import_prefix = IMPORT_PREFIX,
    deps = [
        ":event_filter_proto",
        ":media_type_proto",
        "@com_google_googleapis//google/api:field_behavior_proto",
    ],
)

proto_library(
    name = "media_type_proto",
    srcs = ["media_type.proto"],
    strip_import_prefix = IMPORT_PREFIX,
)

proto_library(
    name = "metric_proto",
    srcs = ["metric.proto"],
    strip_import_prefix = IMPORT_PREFIX,
    deps = [
        "@com_google_googleapis//google/api:field_behavior_proto",
        "@com_google_googleapis//google/api:resource_proto",
        "@com_google_googleapis//google/type:interval_proto",
        "@com_google_protobuf//:duration_proto",
        "@com_google_protobuf//:timestamp_proto",
    ],
)

proto_library(
    name = "metric_calculation_spec_proto",
    srcs = ["metric_calculation_spec.proto"],
    strip_import_prefix = IMPORT_PREFIX,
    deps = [
        ":metric_proto",
        "@com_google_googleapis//google/api:field_behavior_proto",
        "@com_google_googleapis//google/api:resource_proto",
        "@com_google_googleapis//google/type:dayofweek_proto",
    ],
)

proto_library(
    name = "metric_frequency_spec_proto",
    srcs = ["metric_frequency_spec.proto"],
    strip_import_prefix = IMPORT_PREFIX,
    deps = [
        "@com_google_googleapis//google/api:field_behavior_proto",
        "@com_google_googleapis//google/type:dayofweek_proto",
    ],
)

proto_library(
    name = "report_proto",
    srcs = ["report.proto"],
    strip_import_prefix = IMPORT_PREFIX,
    deps = [
        ":metric_proto",
        ":time_interval_proto",
        "@com_google_googleapis//google/api:field_behavior_proto",
        "@com_google_googleapis//google/api:resource_proto",
        "@com_google_googleapis//google/type:date_proto",
        "@com_google_googleapis//google/type:datetime_proto",
        "@com_google_googleapis//google/type:interval_proto",
        "@com_google_protobuf//:timestamp_proto",
    ],
)

proto_library(
    name = "report_schedule_proto",
    srcs = ["report_schedule.proto"],
    strip_import_prefix = IMPORT_PREFIX,
    deps = [
        ":report_proto",
        "@com_google_googleapis//google/api:field_behavior_proto",
        "@com_google_googleapis//google/api:resource_proto",
        "@com_google_googleapis//google/type:date_proto",
        "@com_google_googleapis//google/type:datetime_proto",
        "@com_google_googleapis//google/type:dayofweek_proto",
        "@com_google_protobuf//:timestamp_proto",
    ],
)

proto_library(
    name = "report_schedule_iteration_proto",
    srcs = ["report_schedule_iteration.proto"],
    strip_import_prefix = IMPORT_PREFIX,
    deps = [
        "@com_google_googleapis//google/api:field_behavior_proto",
        "@com_google_googleapis//google/api:resource_proto",
        "@com_google_protobuf//:timestamp_proto",
    ],
)

proto_library(
    name = "reporting_impression_qualification_filter_proto",
    srcs = ["reporting_impression_qualification_filter.proto"],
    strip_import_prefix = IMPORT_PREFIX,
    deps = [
        ":impression_qualification_filter_spec_proto",
        "@com_google_googleapis//google/api:field_behavior_proto",
        "@com_google_googleapis//google/api:resource_proto",
    ],
)

proto_library(
    name = "reporting_interval_proto",
    srcs = ["reporting_interval.proto"],
    strip_import_prefix = IMPORT_PREFIX,
    deps = [
        "@com_google_googleapis//google/api:field_behavior_proto",
        "@com_google_googleapis//google/type:date_proto",
        "@com_google_googleapis//google/type:datetime_proto",
    ],
)

proto_library(
    name = "reporting_unit_proto",
    srcs = ["reporting_unit.proto"],
    strip_import_prefix = IMPORT_PREFIX,
    deps = [
        "@com_google_googleapis//google/api:field_behavior_proto",
        "@com_google_googleapis//google/api:resource_proto",
    ],
)

proto_library(
    name = "reporting_set_proto",
    srcs = ["reporting_set.proto"],
    strip_import_prefix = IMPORT_PREFIX,
    deps = [
        "@com_google_googleapis//google/api:field_behavior_proto",
        "@com_google_googleapis//google/api:resource_proto",
    ],
)

proto_library(
    name = "result_group_proto",
    srcs = ["result_group.proto"],
    strip_import_prefix = IMPORT_PREFIX,
    deps = [
        ":event_filter_proto",
        ":event_template_field_proto",
        ":metric_frequency_spec_proto",
        ":reporting_impression_qualification_filter_proto",
        ":reporting_unit_proto",
        "@com_google_googleapis//google/api:field_behavior_proto",
        "@com_google_googleapis//google/api:resource_proto",
        "@com_google_googleapis//google/type:dayofweek_proto",
        "@com_google_protobuf//:timestamp_proto",
    ],
)

proto_library(
    name = "time_interval_proto",
    srcs = ["time_interval.proto"],
    strip_import_prefix = IMPORT_PREFIX,
    deps = [
        "@com_google_googleapis//google/api:field_behavior_proto",
        "@com_google_googleapis//google/type:interval_proto",
    ],
)

proto_library(
    name = "page_token_proto",
    srcs = ["page_token.proto"],
    deps = [
        "@com_google_protobuf//:timestamp_proto",
    ],
)

# Services.

SERVICE_LIBS = [
    ":basic_reports_service_proto",
    ":event_groups_service_proto",
    ":impression_qualification_filters_service_proto",
    ":metric_calculation_specs_service_proto",
    ":metrics_service_proto",
    ":report_schedule_iterations_service_proto",
    ":report_schedules_service_proto",
    ":reporting_sets_service_proto",
    ":reports_service_proto",
]

proto_library(
    name = "basic_reports_service_proto",
    srcs = ["basic_reports_service.proto"],
    strip_import_prefix = IMPORT_PREFIX,
    deps = [
        ":basic_report_proto",
        "@com_google_googleapis//google/api:annotations_proto",
        "@com_google_googleapis//google/api:client_proto",
        "@com_google_googleapis//google/api:field_behavior_proto",
        "@com_google_googleapis//google/api:field_info_proto",
        "@com_google_googleapis//google/api:resource_proto",
        "@com_google_protobuf//:timestamp_proto",
    ],
)

proto_library(
    name = "event_groups_service_proto",
    srcs = ["event_groups_service.proto"],
    strip_import_prefix = IMPORT_PREFIX,
    deps = [
        ":event_group_proto",
        ":media_type_proto",
        "@com_google_googleapis//google/api:annotations_proto",
        "@com_google_googleapis//google/api:client_proto",
        "@com_google_googleapis//google/api:field_behavior_proto",
        "@com_google_googleapis//google/api:resource_proto",
        "@com_google_protobuf//:timestamp_proto",
    ],
)

proto_library(
    name = "impression_qualification_filters_service_proto",
    srcs = ["impression_qualification_filters_service.proto"],
    strip_import_prefix = IMPORT_PREFIX,
    deps = [
        ":impression_qualification_filter_proto",
        "@com_google_googleapis//google/api:annotations_proto",
        "@com_google_googleapis//google/api:client_proto",
        "@com_google_googleapis//google/api:field_behavior_proto",
        "@com_google_googleapis//google/api:resource_proto",
        "@com_google_protobuf//:timestamp_proto",
    ],
)

proto_library(
    name = "metrics_service_proto",
    srcs = ["metrics_service.proto"],
    strip_import_prefix = IMPORT_PREFIX,
    deps = [
        ":metric_proto",
        "@com_google_googleapis//google/api:annotations_proto",
        "@com_google_googleapis//google/api:client_proto",
        "@com_google_googleapis//google/api:field_behavior_proto",
        "@com_google_googleapis//google/api:field_info_proto",
        "@com_google_googleapis//google/api:resource_proto",
    ],
)

proto_library(
    name = "metric_calculation_specs_service_proto",
    srcs = ["metric_calculation_specs_service.proto"],
    strip_import_prefix = IMPORT_PREFIX,
    deps = [
        ":metric_calculation_spec_proto",
        "@com_google_googleapis//google/api:annotations_proto",
        "@com_google_googleapis//google/api:client_proto",
        "@com_google_googleapis//google/api:field_behavior_proto",
        "@com_google_googleapis//google/api:resource_proto",
    ],
)

proto_library(
    name = "reports_service_proto",
    srcs = ["reports_service.proto"],
    strip_import_prefix = IMPORT_PREFIX,
    deps = [
        ":report_proto",
        "@com_google_googleapis//google/api:annotations_proto",
        "@com_google_googleapis//google/api:client_proto",
        "@com_google_googleapis//google/api:field_behavior_proto",
        "@com_google_googleapis//google/api:resource_proto",
    ],
)

proto_library(
    name = "report_schedules_service_proto",
    srcs = ["report_schedules_service.proto"],
    strip_import_prefix = IMPORT_PREFIX,
    deps = [
        ":report_schedule_proto",
        "@com_google_googleapis//google/api:annotations_proto",
        "@com_google_googleapis//google/api:client_proto",
        "@com_google_googleapis//google/api:field_behavior_proto",
        "@com_google_googleapis//google/api:resource_proto",
    ],
)

proto_library(
    name = "report_schedule_iterations_service_proto",
    srcs = ["report_schedule_iterations_service.proto"],
    strip_import_prefix = IMPORT_PREFIX,
    deps = [
        ":report_schedule_iteration_proto",
        "@com_google_googleapis//google/api:annotations_proto",
        "@com_google_googleapis//google/api:client_proto",
        "@com_google_googleapis//google/api:field_behavior_proto",
        "@com_google_googleapis//google/api:resource_proto",
    ],
)

proto_library(
    name = "reporting_sets_service_proto",
    srcs = ["reporting_sets_service.proto"],
    strip_import_prefix = IMPORT_PREFIX,
    deps = [
        ":reporting_set_proto",
        "@com_google_googleapis//google/api:annotations_proto",
        "@com_google_googleapis//google/api:client_proto",
        "@com_google_googleapis//google/api:field_behavior_proto",
        "@com_google_googleapis//google/api:resource_proto",
    ],
)

# Language-specific libraries.

[
    kt_jvm_proto_library(
        name = "{prefix}_kt_jvm_proto".format(prefix = proto_lib_prefix(label_str)),
        deps = [label_str],
    )
    for label_str in MESSAGE_LIBS
]

[
    kt_jvm_grpc_proto_library(
        name = "{prefix}_kt_jvm_grpc_proto".format(prefix = proto_lib_prefix(label_str)),
        deps = [label_str],
    )
    for label_str in SERVICE_LIBS
]

[
    protoc_gen_openapiv2(
        name = "{prefix}_openapi".format(prefix = proto_lib_prefix(label_str)),
        proto = label_str,
    )
    for label_str in SERVICE_LIBS
]

# OpenAPI specifications for services exposed by the Reporting grpc-gateway.
filegroup(
    name = "exposed_openapi_specs",
    srcs = [
        ":basic_reports_service_openapi",
        ":event_groups_service_openapi",
        ":impression_qualification_filters_service_openapi",
        ":reporting_sets_service_openapi",
    ],
)

GO_IMPORT_PATH = "github.com/world-federation-of-advertisers/cross-media-measurement/reporting/apiv2alpha/reportingpb"

GO_DEPS = [
    "@org_golang_google_genproto//googleapis/type/date",
    "@org_golang_google_genproto//googleapis/type/datetime",
    "@org_golang_google_genproto//googleapis/type/dayofweek",
    "@org_golang_google_genproto//googleapis/type/interval",
    "@org_golang_google_genproto_googleapis_api//annotations",
    "@com_google_cloud_go_longrunning//autogen/longrunningpb",
]

go_proto_library(
    name = "reportingpb_go_proto",
    importpath = GO_IMPORT_PATH,
    protos = MESSAGE_LIBS,
    deps = GO_DEPS,
)

go_grpc_gateway_proto_library(
    name = "reportingpb_go_grpc_proto",
    embed = [":reportingpb_go_proto"],
    importpath = GO_IMPORT_PATH,
    protos = SERVICE_LIBS,
    deps = GO_DEPS,
)
