load("//build:variables.bzl", "TEST_K8S_SETTINGS")
load("//build/k8s:defs.bzl", "k8s_apply", "kind_load_image", "kind_load_images")
load("@wfa_rules_cue//cue:defs.bzl", "cue_library")
load("//src/main/k8s:macros.bzl", "cue_dump")
load("//src/main/docker:images.bzl", "ALL_LOCAL_IMAGES")

cue_library(
    name = "config",
    srcs = ["config.cue"],
    deps = [
        "//src/main/k8s:base",
        "//src/main/k8s:spanner",
    ],
)

cue_dump(
    name = "emulators",
    srcs = ["emulators.cue"],
    cue_tags = {
        "secret_name": TEST_K8S_SETTINGS.secret_name,
    },
    tags = ["manual"],
    deps = [
        "//src/main/k8s:base",
    ],
)

cue_dump(
    name = "kingdom",
    srcs = ["kingdom.cue"],
    cue_tags = {
        "secret_name": TEST_K8S_SETTINGS.secret_name,
    },
    tags = ["manual"],
    deps = [
        ":config",
        "//src/main/k8s:kingdom",
    ],
)

cue_dump(
    name = "resource_setup",
    srcs = ["resource_setup.cue"],
    cue_tags = {
        "secret_name": TEST_K8S_SETTINGS.secret_name,
    },
    tags = ["manual"],
    deps = [
        "//src/main/k8s:resource_setup",
    ],
)

cue_dump(
    name = "duchies",
    srcs = ["duchies.cue"],
    cue_tags = {
        "secret_name": TEST_K8S_SETTINGS.secret_name,
        "aggregator_cert_name": TEST_K8S_SETTINGS.aggregator_cert_name,
        "worker1_cert_name": TEST_K8S_SETTINGS.worker1_cert_name,
        "worker2_cert_name": TEST_K8S_SETTINGS.worker2_cert_name,
    },
    tags = ["manual"],
    deps = [
        ":config",
        "//src/main/k8s:duchy",
    ],
)

cue_dump(
    name = "edp_simulators",
    srcs = ["edp_simulators.cue"],
    cue_tags = {
        "secret_name": TEST_K8S_SETTINGS.secret_name,
        "mc_name": TEST_K8S_SETTINGS.mc_name,
        "edp1_name": TEST_K8S_SETTINGS.edp1_name,
        "edp2_name": TEST_K8S_SETTINGS.edp2_name,
        "edp3_name": TEST_K8S_SETTINGS.edp3_name,
        "edp4_name": TEST_K8S_SETTINGS.edp4_name,
        "edp5_name": TEST_K8S_SETTINGS.edp5_name,
        "edp6_name": TEST_K8S_SETTINGS.edp6_name,
    },
    tags = ["manual"],
    deps = [
        "//src/main/k8s:edp_simulator",
    ],
)

cue_dump(
    name = "mc_frontend_simulator",
    srcs = ["frontend_simulator.cue"],
    cue_tags = {
        "secret_name": TEST_K8S_SETTINGS.secret_name,
        "mc_name": TEST_K8S_SETTINGS.mc_name,
        "mc_api_key": TEST_K8S_SETTINGS.mc_api_key,
    },
    tags = ["manual"],
    deps = [
        "//src/main/k8s:frontend_simulator",
    ],
)

[kind_load_image(
    name = "load_" + image.name,
    image_archive = image.image + ".tar",
    tags = ["manual"],
) for image in ALL_LOCAL_IMAGES]

EMULATOR_IMAGE_IMPORTS = [":load_fake_storage_server_image"]

kind_load_images(
    name = "load_emulator_images",
    tags = ["manual"],
    deps = EMULATOR_IMAGE_IMPORTS,
)

k8s_apply(
    name = "emulators_kind",
    src = ":emulators",
    # Deploying emulators should bring down everything else since DB and storage
    # get recreated.
    delete_selector = "app.kubernetes.io/part-of=halo-cmms",
    imports = EMULATOR_IMAGE_IMPORTS,
    tags = ["manual"],
)

RESOURCE_SETUP_IMAGE_IMPORTS = [":load_resource_setup_runner_image"]

kind_load_images(
    name = "load_resource_setup_images",
    tags = ["manual"],
    deps = RESOURCE_SETUP_IMAGE_IMPORTS,
)

k8s_apply(
    name = "resource_setup_kind",
    src = ":resource_setup",
    imports = RESOURCE_SETUP_IMAGE_IMPORTS,
    tags = ["manual"],
)

KINGDOM_IMAGE_NAMES = [
    "kingdom_spanner_update_schema_image",
    "kingdom_data_server_image",
    "kingdom_system_api_server_image",
    "kingdom_v2alpha_public_api_server_image",
]

KINGDOM_IMAGE_IMPORTS = [
    ":load_" + image_name
    for image_name in KINGDOM_IMAGE_NAMES
]

kind_load_images(
    name = "load_kingdom_images",
    tags = ["manual"],
    deps = KINGDOM_IMAGE_IMPORTS,
)

k8s_apply(
    name = "kingdom_kind",
    src = ":kingdom",
    delete_selector = "app.kubernetes.io/part-of=halo-cmms,app.kubernetes.io/component=kingdom",
    imports = KINGDOM_IMAGE_IMPORTS,
    tags = ["manual"],
)

DUCHY_IMAGE_NAMES = [
    "duchy_spanner_update_schema_image",
    "duchy_async_computation_control_server_image",
    "duchy_spanner_computations_server_image",
    "duchy_herald_daemon_image",
    "forwarded_storage_computation_control_server_image",
    "forwarded_storage_liquid_legions_v2_mill_daemon_image",
    "forwarded_storage_requisition_fulfillment_server_image",
]

DUCHY_IMAGE_IMPORTS = [
    ":load_" + image_name
    for image_name in DUCHY_IMAGE_NAMES
]

kind_load_images(
    name = "load_duchy_images",
    tags = ["manual"],
    deps = DUCHY_IMAGE_IMPORTS,
)

k8s_apply(
    name = "duchies_kind",
    src = ":duchies",
    delete_selector = "app.kubernetes.io/part-of=halo-cmms,app.kubernetes.io/component=duchy",
    imports = DUCHY_IMAGE_IMPORTS,
    tags = ["manual"],
)

EDP_SIMULATOR_IMAGE_IMPORTS = [
    ":load_forwarded_storage_edp_simulator_runner_image",
]

kind_load_images(
    name = "load_edp_simulator_images",
    tags = ["manual"],
    deps = EDP_SIMULATOR_IMAGE_IMPORTS,
)

k8s_apply(
    name = "edp_simulators_kind",
    src = ":edp_simulators",
    delete_selector = "app.kubernetes.io/part-of=halo-cmms,app.kubernetes.io/component=simulator",
    imports = EDP_SIMULATOR_IMAGE_IMPORTS,
    tags = ["manual"],
)

FRONTEND_SIMULATOR_IMAGE_IMPORTS = [
    ":load_forwarded_storage_frontend_simulator_runner_image",
]

kind_load_images(
    name = "load_mc_frontend_simulator_images",
    tags = ["manual"],
    deps = FRONTEND_SIMULATOR_IMAGE_IMPORTS,
)

k8s_apply(
    name = "mc_frontend_simulator_kind",
    src = ":mc_frontend_simulator",
    imports = FRONTEND_SIMULATOR_IMAGE_IMPORTS,
    tags = ["manual"],
)
