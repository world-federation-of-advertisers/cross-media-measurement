load("//build/cue:defs.bzl", "cue_string_field")
load("//build/k8s:defs.bzl", "k8s_apply", "k8s_import")
load("//build:variables.bzl", "IMAGE_REPOSITORY_SETTINGS", "TEST_GOOGLE_CLOUD_SETTINGS")
load("//src/main/docker:images.bzl", "ALL_LOCAL_IMAGES")
load(":macros.bzl", "cue_dump")

exports_files(["duchy_rpc_config.textproto"])

exports_files(["duchy_id_config.textproto"])

exports_files(["public_api_protocol_configs.textproto"])

exports_files(["aggregator_protocols_setup_config.textproto"])

exports_files(["non_aggregator_protocols_setup_config.textproto"])

cue_string_field(
    name = "duchy_rpc_config",
    src = "duchy_rpc_config.textproto",
    identifier = "#DuchyInfoConfig",
    package = "k8s",
)

cue_string_field(
    name = "duchy_id_config",
    src = "duchy_id_config.textproto",
    identifier = "#DuchyIdConfig",
    package = "k8s",
)

cue_string_field(
    name = "public_api_protocol_configs",
    src = "public_api_protocol_configs.textproto",
    identifier = "#PublicApiProtocolConfigs",
    package = "k8s",
)

cue_string_field(
    name = "aggregator_protocols_setup_config",
    src = "aggregator_protocols_setup_config.textproto",
    identifier = "#AggregatorProtocolsSetupConfig",
    package = "k8s",
)

cue_string_field(
    name = "non_aggregator_protocols_setup_config",
    src = "non_aggregator_protocols_setup_config.textproto",
    identifier = "#NonAggregatorProtocolsSetupConfig",
    package = "k8s",
)

CUE_LIBRARIES = [
    ":aggregator_protocols_setup_config.cue",
    ":non_aggregator_protocols_setup_config.cue",
    ":duchy_rpc_config.cue",
    ":duchy_id_config.cue",
    ":public_api_protocol_configs.cue",
    "base.cue",
    "duchy.cue",
    "kingdom.cue",
]

cue_dump(
    name = "kingdom_and_three_duchies_from_cue_local",
    srcs = CUE_LIBRARIES + ["kingdom_and_three_duchies_local.cue"],
)

cue_dump(
    name = "kingdom_and_three_duchies_from_cue_gke",
    srcs = CUE_LIBRARIES + ["kingdom_and_three_duchies_gke.cue"],
    cue_tags = {
        "cloud_storage_bucket": TEST_GOOGLE_CLOUD_SETTINGS.cloud_storage_bucket,
        "cloud_storage_project": TEST_GOOGLE_CLOUD_SETTINGS.cloud_storage_project,
        "container_registry": IMAGE_REPOSITORY_SETTINGS.container_registry,
        "repository_prefix": IMAGE_REPOSITORY_SETTINGS.repository_prefix,
        "spanner_instance": TEST_GOOGLE_CLOUD_SETTINGS.spanner_instance,
        "spanner_project": TEST_GOOGLE_CLOUD_SETTINGS.spanner_project,
    },
)

ALL_LOCAL_IMAGE_ARCHIVES = [
    image_spec.image + ".tar"
    for image_spec in ALL_LOCAL_IMAGES
]

filegroup(
    name = "all_archives",
    srcs = ALL_LOCAL_IMAGE_ARCHIVES,
    tags = ["manual"],
)

filegroup(
    name = "k8s_deployment_config",
    srcs = [
        ":kingdom_and_three_duchies_from_cue_gke.yaml",
        ":kingdom_and_three_duchies_from_cue_local.yaml",
    ],
    data = [
        ":all_archives",
    ],
    tags = ["manual"],
    visibility = [":k8s_deployer"],
)

package_group(
    name = "k8s_deployer",
    packages = [
        "//src/main/kotlin/org/wfanet/measurement/tools/...",
        "//src/test/kotlin/org/wfanet/measurement/e2e/...",
    ],
)

SELECTOR = "app.kubernetes.io/name=measurement-system"

[k8s_import(
    name = "import_" + image.name + "_kind",
    image_archive = image.image + ".tar",
    k8s_environment = "kind",
    tags = ["manual"],
) for image in ALL_LOCAL_IMAGES]

[k8s_import(
    name = "import_" + image.name + "_u7s",
    image_archive = image.image + ".tar",
    k8s_environment = "usernetes-containerd",
    tags = ["manual"],
) for image in ALL_LOCAL_IMAGES]

k8s_apply(
    name = "kingdom_and_three_duchies_kind",
    src = ":kingdom_and_three_duchies_from_cue_local.yaml",
    delete_selector = SELECTOR,
    imports = [":import_" + image.name + "_kind" for image in ALL_LOCAL_IMAGES],
    tags = ["manual"],
)

k8s_apply(
    name = "kingdom_and_three_duchies_u7s",
    src = ":kingdom_and_three_duchies_from_cue_local.yaml",
    delete_selector = SELECTOR,
    imports = [":import_" + image.name + "_u7s" for image in ALL_LOCAL_IMAGES],
    tags = ["manual"],
)
